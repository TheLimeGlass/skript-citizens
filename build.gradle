import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.gradleup.shadow' version '9.0.2'
	id 'xyz.jpenilla.run-paper' version '2.3.1'
    id 'java'
}

tasks {
    runServer {
        minecraftVersion("1.21.5")
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

def supportedJava = 21

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(supportedJava))
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs += ['-source', '' + supportedJava, '-target', '' + supportedJava]
}

repositories {
    mavenCentral()

    //Spigot
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    // Bungeecord
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }

    // Skript
    maven {
        url 'https://repo.skriptlang.org/releases'
    }

    //Citizens
    maven {
        name = 'citizens-repo'
        url = 'https://maven.citizensnpcs.co/repo'
    }

}

configurations {
	shadow
	implementation.extendsFrom shadow
}

dependencies {

    //Nullable annotation
    implementation (group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.annotation', version: '2.2.700')

    //Spigot/Bukkit
    implementation (group: 'org.spigotmc', name: 'spigot-api', version: '1.21.8-R0.1-SNAPSHOT')

    //Skript
    implementation (group: 'com.github.SkriptLang', name: 'Skript', version: '2.7.0-beta3') {
        transitive = false
    }

	//GSON bundled with Minecraft, should be kept in sync
	//https://hub.spigotmc.org/stash/projects/SPIGOT/repos/bukkit/browse/pom.xml
    implementation (group: 'com.google.code.gson', name: 'gson', version: '2.10.1')

    // bStats
    shadow (group: 'org.bstats', name: 'bstats-bukkit', version: '3.0.2')

    //Citizens
    compileOnly('net.citizensnpcs:citizens-main:2.0.39-SNAPSHOT') {
        exclude group: '*', module: '*'
    }

}

processResources {
    filter ReplaceTokens, tokens: ["version": project.property("version")]
    from ("lang/") {
        include '*.lang'
        into 'lang/'
    }
}

shadowJar {
    dependencies {
        include(dependency('org.bstats:bstats-bukkit'))
        include(dependency('org.bstats:bstats-base'))
    }
	configurations = [project.configurations.shadow]
	archiveFileName = "${project.name}-${project.version}.jar"
    relocate 'org.bstats', 'ca.nylhus.skriptcitizens.bstats'
    minimize()
}

processResources {
	filter ReplaceTokens, tokens: [
		'version' : project.version,
	]
}
